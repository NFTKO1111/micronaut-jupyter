plugins {
    // Apply the groovy plugin to add support for Groovy
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

repositories {
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "org.codehaus.groovy:groovy-all:$groovyVersion"

    implementation "io.micronaut:micronaut-inject-groovy:$micronautVersion"
    implementation "io.micronaut:micronaut-inject-java:$micronautVersion"
    implementation "io.micronaut:micronaut-messaging:$micronautVersion"
    implementation "io.micronaut:micronaut-inject:$micronautVersion"
    implementation "io.micronaut:micronaut-management:$micronautVersion"

    compile "com.github.twosigma.beakerx:beaker-kernel-base:$beakerxVersion"
    compile "com.github.twosigma.beakerx:beaker-kernel-groovy:$beakerxVersion"

    testImplementation "org.codehaus.groovy:groovy-test:$groovyVersion"

    // Use the awesome Spock testing and specification framework
    testImplementation "org.spockframework:spock-core:$spockVersion"
    testImplementation "org.testcontainers:spock:1.12.1"

    testImplementation "io.micronaut.test:micronaut-test-core:$micronautTestVersion"
    testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"
    testImplementation "io.micronaut.test:micronaut-test-junit5:$micronautTestVersion"
    testImplementation "io.micronaut:micronaut-inject-groovy:$micronautVersion"
    testImplementation "io.micronaut:micronaut-inject-java:$micronautVersion"
    testImplementation "io.micronaut:micronaut-http-server-netty:$micronautVersion"
    testImplementation "io.micronaut:micronaut-http-client:$micronautVersion"

    testImplementation "org.objenesis:objenesis:1.4"
    testImplementation "ch.qos.logback:logback-classic:1.2.3"

}

test {
    testLogging {
        showStandardStreams = true
    }
    failFast = false
}

publishing {
    publications {
        jupyterConfigurationLibrary(MavenPublication) {
            artifactId = 'micronaut-jupyter'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = artifactId
                description = projectDesc
                url = projectUrl
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = "Joshua Carter"
                        email = "jcarter@stainless.ai"
                        organization = "Stainless.Ai"
                        organizationUrl = "http://www.stainless.ai"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/${githubSlug}.git"
                    developerConnection = "scm:git:ssh://github.com:${githubSlug}.git"
                    url = "http://github.com/${githubSlug}/tree/$githubBranch"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.jupyterConfigurationLibrary
}
